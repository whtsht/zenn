---
title: "Rascalã§ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ã‚’ä½œã‚‹"
emoji: "ğŸ¾"
type: "tech"
topics: ["rascal", "ãƒ¡ã‚¿ãƒ—ãƒ­ã‚°ãƒ©ãƒŸãƒ³ã‚°"]
published: true
---

## ã¯ã˜ã‚ã«

[å‰å›](https://zenn.dev/whiteshirt/articles/rascal-recipes)ã€Rascalã¨ã„ã†ãƒ¡ã‚¿ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ã®ç´¹ä»‹ã—ã¦ã€ç°¡å˜ãªè¨ˆç®—æ©Ÿã‚’ä½œã‚Šã¾ã—ãŸã€‚

ä»Šå›ã¯Rascalå…¬å¼ã‚µã‚¤ãƒˆã‚’å‚è€ƒã«ã€ã‚ªãƒªã‚¸ãƒŠãƒ«è¨€èªFuncã®ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ã‚’ä½œã£ã¦ã„ã“ã†ã¨æ€ã„ã¾ã™ã€‚

Funcã¯å¤‰æ•°ã®æŸç¸›ã€å¼ã€é–¢æ•°ãªã©ãŒã‚ã‚‹é–¢æ•°å‹è¨€èªã§ã™ã€‚ä½¿ãˆã‚‹ãƒ‡ãƒ¼ã‚¿å‹ã¯è‡ªç„¶æ•°ã®ã¿ã§ã™ã€‚

ä¾‹ãˆã°ã€Funcã§éšä¹—ã‚’æ±‚ã‚ã‚‹ã‚³ãƒ¼ãƒ‰ã¯ä»¥ä¸‹ã®ã‚ˆã†ã«æ›¸ã‘ã¾ã™ã€‚

```haskell:fact.func
fact(n) =
    if n == 0
        then 1
        else n * fact(n - 1)
    end
```

ã“ã®è¨˜äº‹ã§ã€Rascalã§ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ã‚’ä½œã‚‹é›°å›²æ°—ã‚’å‘³ã‚ã£ã¦ã»ã—ã„ã§ã™ã€‚

## æ–‡æ³•

`Syntax.rsc`ã«æ–‡æ³•ã®å®šç¾©ã‚’æ›¸ãã¾ã™ã€‚

Funcè¨€èªã¯ä»¥ä¸‹ã®æ§‹æˆã«ãªã£ã¦ã„ã¾ã™ã€‚

- `Prog`: ãƒ—ãƒ­ã‚°ãƒ©ãƒ å…¨ä½“ã€`Func`ã®é›†ã¾ã‚Š
- `Func`: é–¢æ•°ã®å®šç¾©ã€é–¢æ•°åã€å¼•æ•°ã€`Exp`ã‚’æŒã¤
- `Exp` : å¼ã®å®šç¾©ã€å››å‰‡æ¼”ç®—ã‚„`let in`å¼ã‚„`if then else`å¼ã‚’ã‚µãƒãƒ¼ãƒˆ

ã¾ãŸ`%%` ã§1è¡Œã‚³ãƒ¡ãƒ³ãƒˆã€`%`ã§å›²ã†ã¨è¤‡æ•°è¡Œã‚³ãƒ¡ãƒ³ãƒˆæ‰±ã„ã«ãªã‚Šã¾ã™ã€‚

```rascal:Syntax.rsc
module Syntax

lexical Ident = [a-z][a-z0-9]* !>> [a-z0-9];

lexical Natural = [0-9]+ ;

lexical String = "\"" ![\"]*  "\"";

layout Layout = WhitespaceAndComment* !>> [\ \t\n\r];

lexical WhitespaceAndComment
   = [\ \t\n\r]
   | @category="Comment" ws2: "%" ![%]+ "%"
   | @category="Comment" ws3: "%%" ![\n]* $
   ;

syntax Binding = binding: Ident name "=" Exp exp;

syntax Exp
    = natCon: Natural
    | bracket "(" Exp ")"
    > left (mul: Exp e1 "*" Exp e2 | div: Exp e1 "/" Exp e2)
    > left (sub: Exp e1 "-" Exp e2 | add: Exp e1 "+" Exp e2)
    > left ( gt: Exp e1 "\>" Exp e2
           | lt: Exp e1 "\<" Exp e2
           | ge: Exp e1 "\>=" Exp e2
           | le: Exp e1 "\<=" Exp e2
           )
    > left (eq: Exp e1 "==" Exp e2 | neq: Exp e1 "/=" Exp e2)
    | id: Ident name
    | let: "let" {Binding ","}* binds "in" Exp exp "end"
    | cond: "if" Exp condExp "then" Exp thenExp "else" Exp elseExp "end"
    | call: Ident name "(" {Exp ","}* args ")"
    ;

syntax Func = func: Ident name "(" {Ident ","}* args ")" "=" Exp;

start syntax Prog = prog: Func*;
```

## Syntaxã‹ã‚‰Rascalã®ãƒ‡ãƒ¼ã‚¿æ§‹é€ ã«å¤‰æ›

å®Ÿã¯[å‰å›](https://zenn.dev/whiteshirt/articles/rascal-recipes)ã®è¨ˆç®—æ©Ÿã¯ã“ã®å·¥ç¨‹ã‚’çœã„ã¦ã€ç›´æ¥syntaxã®å®šç¾©ã‹ã‚‰è©•ä¾¡ã—ã¦ã„ã¾ã™ã€‚ã‚ˆã‚Šè¤‡é›‘ãªå‡¦ç†ã‚’ã™ã‚‹å ´åˆã¯Rascalã®ãƒ‡ãƒ¼ã‚¿æ§‹é€ ã«å¤‰æ›ã—ã¦ã‹ã‚‰ã®ã»ã†ãŒè‰¯ã„ã§ã™ã€‚

`Abstract.rsc`ã«Funcè¨€èªã§æ›¸ã‹ã‚ŒãŸãƒ—ãƒ­ã‚°ãƒ©ãƒ ã‚’è¡¨ã™ãƒ‡ãƒ¼ã‚¿æ§‹é€ ã‚’å®šç¾©ã—ã¾ã™ã€‚

```rascal:Abstract.rsc
module Abstract

data BINDING
    = binding(str name, EXP exp);

data EXP
    = natCon(int iVal)
    | mul(EXP e1, EXP e2)
    | div(EXP e1, EXP e2)
    | sub(EXP e1, EXP e2)
    | add(EXP e1, EXP e2)
    | gt(EXP e1, EXP e2)
    | lt(EXP e1, EXP e2)
    | ge(EXP e1, EXP e2)
    | le(EXP e1, EXP e2)
    | eq(EXP e1, EXP e2)
    | neq(EXP e1, EXP e2)
    | id(str name)
    | let(list[BINDING] binds, EXP exp)
    | cond(EXP condExp, EXP thenExp, EXP elseExp)
    | call(str name, list[EXP] args)
    ;

data FUNC
    = func(str name, list[str] args, EXP exp);

data PROG
    = prog(list[FUNC] funcs);
```

ã‚ˆãƒ¼ãè¦‹ã‚‹ã¨`Syntax.rsc`ã®`Prog`ã€`Exp`ã€`Binding`ã¨`Abstract.rsc`ã®`PROG`ã€`EXP`ã€`BINDING`ã¯å¯¾å¿œé–¢ä¿‚ãŒã‚ã‚‹ã“ã¨ãŒã‚ã‹ã‚Šã¾ã™ã€‚

ä¾‹ãˆã°`Syntax.rsc`ã®`Exp`ã®å®šç¾©ã®1ã¤ã§ã‚ã‚‹

```
cond: "if" Exp condExp "then" Exp thenExp "else" Exp elseExp "end"
```

ã¯`Abstract.rsc`ã®`EXP`ã®å®šç¾©ã®1ã¤ã§ã‚ã‚‹

```
cond(EXP condExp, EXP thenExp, EXP elseExp)
```

ã¨å¯¾å¿œã—ã¦ã„ã¾ã™ã€‚

ã“ã®å¯¾å¿œã‚’æ¼ã‚Œãªãæ›¸ã‹ãªã„ã¨ã‚¨ãƒ©ãƒ¼ã«ãªã‚‹ã®ã§æ³¨æ„ã—ã¦ä¸‹ã•ã„ã€‚

`Load.rsc`ã«Funcã‚½ãƒ¼ã‚¹ã‚³ãƒ¼ãƒ‰ã‹ã‚‰`PROG`ãƒ‡ãƒ¼ã‚¿æ§‹é€ ã‚’ç”Ÿæˆã™ã‚‹ã‚³ãƒ¼ãƒ‰ã‚’æ›¸ãã¾ã™ã€‚

```rascal:Load.rsc
module Load

import Prelude;
import Syntax;
import Abstract;

PROG load(str s) = implode(#PROG, parse(#start[Prog], s).top);
PROG load(loc l) = implode(#PROG, parse(#start[Prog], l).top);
```

ã“ã“ã§ä»•äº‹ã‚’ã—ã¦ã‚‹ã®ã¯`implode`ã¨ã„ã†é–¢æ•°ã§ã™ã€‚`parse`é–¢æ•°ã§ã‚½ãƒ¼ã‚¹ã‚³ãƒ¼ãƒ‰ã‚’`Prog`ã«å¤‰æ›å¾Œã€`implode`é–¢æ•°ã§`PROG`ãƒ‡ãƒ¼ã‚¿æ§‹é€ ã«å¤‰æ›ã—ã¦ã„ã¾ã™ã€‚

`load`é–¢æ•°ã¯ã‚½ãƒ¼ã‚¹ã‚³ãƒ¼ãƒ‰ã‚’ç›´æ¥æ¸¡ã™ã“ã¨ã‚‚ã§ãã¾ã™ãŒã€`Location`ã‚’æŒ‡å®šã™ã‚‹ã“ã¨ã§ãƒ•ã‚¡ã‚¤ãƒ«ã‚’å…¥åŠ›ã«ã§ãã¾ã™ã€‚

`Location`ã¯Rascalã«å‡ºã¦ãã‚‹æ¦‚å¿µã§ã€ãƒ•ã‚¡ã‚¤ãƒ«ãƒ‘ã‚¹ã‚’ä¸€èˆ¬åŒ–ã—ãŸã‚ˆã†ãªã‚‚ã®ã§ã™ã€‚è©³ã—ãã¯ä»¥ä¸‹ã‚’å‚è€ƒã«ã—ã¦ä¸‹ã•ã„ã€‚

https://www.rascal-mpl.org/docs/Rascal/Expressions/Values/Location/

## VMï¼ˆVirtual Machineï¼‰ã‚³ãƒ¼ãƒ‰

Funcè¨€èªã‹ã‚‰ã‚¹ã‚¿ãƒƒã‚¯ãƒ™ãƒ¼ã‚¹ã®VMãƒã‚·ãƒ³ã§å‹•ãã“ã¨ã‚’æƒ³å®šã—ãŸã‚³ãƒ¼ãƒ‰ã‚’ç”Ÿæˆã—ã¾ã™ã€‚

`Assembly.rsc`ã«ãã®VMãƒã‚·ãƒ³ã§å‹•ãå‘½ä»¤ã‚’å®šç¾©ã—ã¾ã™ã€‚

```rascal:Assembly.rsc
module Assembly

data Instr
    // è‡ªç„¶æ•°ã‚’ã‚¹ã‚¿ãƒƒã‚¯ã«ç©ã‚€
    = pushNat(int intCon)
    // è­˜åˆ¥å­ã‚’ã‚¹ã‚¿ãƒƒã‚¯ã«ç©ã‚€
    | pushId(str id)
    // è­˜åˆ¥å­ã‚’ã‚¹ã‚¿ãƒƒã‚¯ã‹ã‚‰å–ã‚Šå‡ºã™ã€‚ãã®è­˜åˆ¥å­ã®å¤‰æ•°ã‚’ã‚¹ã‚¿ãƒƒã‚¯ã«ç©ã‚€
    | getVal()
    // Assign value on top, to variable at identifier top-1
    | setVal()
    // ãƒ©ãƒ™ãƒ«ã®å®šç¾©
    | label(str label)
    // ç„¡æ¡ä»¶ã§ãƒ©ãƒ™ãƒ«ã«é£›ã¶
    | go(str label)
    // ã‚¹ã‚¿ãƒƒã‚¯ã‹ã‚‰å€¤ã‚’å–ã‚Šå‡ºã™ã€å–ã‚Šå‡ºã—ãŸå€¤ã‚’0ä»¥å¤–ãªã‚‰ã°ãƒ©ãƒ™ãƒ«ã«é£›ã¶
    | goIf(str label)
    // å››å‰‡æ¼”ç®—
    | mul() | div() | add() | sub()
    // æ¯”è¼ƒæ¼”ç®—å­
    | gt() | lt() | ge() | le() | eq() | neq()
    // ã‚¹ã‚¿ãƒƒã‚¯ã‹ã‚‰è­˜åˆ¥å­ã‚’å–ã‚Šå‡ºã—ã¦ãã®è­˜åˆ¥å­ã®é–¢æ•°ã‚’å‘¼ã³å‡ºã™
    | call()
    ;

alias Instrs = list[Instr];

alias Code = map[str, Instrs];
```

ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ã‚’ä½œã‚‹ã“ã¨ãŒç›®çš„ãªã®ã§VMã‚³ãƒ¼ãƒ‰ã¯ãƒ†ã‚­ãƒˆãƒ¼ã«è¨­è¨ˆã—ã¾ã—ãŸã€‚é–¢æ•°ã®å¼•æ•°ã‚„è¨ˆç®—çµæœã‚’ä¿å­˜ã™ã‚‹ã‚¹ã‚¿ãƒƒã‚¯ã¨å¤‰æ•°ã‚’ä¿å­˜ã™ã‚‹ãƒ’ãƒ¼ãƒ—ãŒä¸€å¿œã‚ã‚Šã¾ã™ã€‚ã€Œã§ã‚‚ã“ã‚Œã ã¨å¤‰æ•°ãŒä¸Šæ›¸ãã•ã‚Œã­ï¼Ÿã€ã€Œ ãªã‚“ã®ãŸã‚ã«é–¢æ•°ã®å¼•æ•°ã‚¹ã‚¿ãƒƒã‚¯ã«ç©ã‚“ã§ã‚‹ã‚“ã ã‚ˆã€‚ã€ãªã©ã®ãƒ„ãƒƒã‚³ãƒŸã©ã“ã‚æº€è¼‰ãªã®ã¯ã”å®¹èµ¦ä¸‹ã•ã„ã€‚

## ã‚³ãƒ¼ãƒ‰ç”Ÿæˆ

ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ã¯æ™®é€šã€å¤‰æ•°ãŒå®£è¨€ã•ã‚Œã¦ã„ã‚‹ã‹ãªã©ã‚’ãƒã‚§ãƒƒã‚¯ã—ã¾ã™ãŒã€ä»Šå›ã¯çœãã¾ã™ã€‚é¢å€’ãªã‚“ã§ã€‚

`Compile.rsc`ã«`PROG`ã‹ã‚‰`Code`ã‚’ç”Ÿæˆã™ã‚‹`compileProg`ã‚’å®šç¾©ã—ã¾ã™ã€‚

```rascal:Compile.rsc
module Compile

import Assembly;
import Abstract;
import IO;

Instrs compileBinding(binding(str name, EXP exp))
    = [*compileExp(exp), pushId(name), setVal()];

Instrs compileExp(natCon (int N)) = [pushNat(N)];

Instrs compileExp(id (str name)) = [pushId(name), getVal()];

// å¤‰æ•°ã‚’æŸç¸›ã—ãŸå¾Œã€expã®ã‚³ãƒ¼ãƒ‰ç”Ÿæˆ
Instrs compileExp(let (list[BINDING] binds, EXP exp))
    = [*([*compileBinding(bind) | bind <- binds]), *compileExp(exp)];

// ãƒ©ãƒ™ãƒ«ç”Ÿæˆç”¨
private int nLabel = 0;

// ãƒ©ãƒ™ãƒ«ç”Ÿæˆ
private str nextLabel() {
  nLabel += 1;
  return "L<nLabel>";
}

// åˆ†å²ã™ã‚‹ãŸã‚ã®ãƒ©ãƒ™ãƒ«ã‚’ç”Ÿæˆã™ã‚‹ã€‚goIfã¯ã‚¹ã‚¿ãƒƒã‚¯ã®å…ˆé ­ãŒ0ä»¥å¤–ãªã‚‰åˆ†å²ã€‚
Instrs compileExp(cond(EXP condExp, EXP thenExp, EXP elseExp)) {
    thenLabel = nextLabel();
    endLabel = nextLabel();
    return [
        *compileExp(condExp),
        goIf(thenLabel),
        *compileExp(elseExp),
        go(endLabel),
        label(thenLabel),
        *compileExp(thenExp),
        label(endLabel)
    ];
}

// e1 e2ã‚’è¨ˆç®—ã™ã‚‹ã¨ãã‚Œãã‚Œã®è¨ˆç®—çµæœãŒã‚¹ã‚¿ãƒƒã‚¯ã«æ®‹ã‚‹ã¯ãšã€‚æœ€å¾Œã«mul()ã‚„div()å‘½ä»¤ã‚’å‘¼ã³ã€
// ã‚¹ã‚¿ãƒƒã‚¯ã‹ã‚‰å€¤ã‚’å–ã‚Šå‡ºã—è¨ˆç®—ã‚’è¡Œã„ã€è¨ˆç®—çµæœã‚’å†ã³ã‚¹ã‚¿ãƒƒã‚¯ã«ãƒ—ãƒƒã‚·ãƒ¥ã™ã‚‹ã€‚
Instrs compileExp(mul(EXP e1, EXP e2)) = [*compileExp(e1), *compileExp(e2), mul()];
Instrs compileExp(div(EXP e1, EXP e2)) = [*compileExp(e1), *compileExp(e2), div()];
Instrs compileExp(sub(EXP e1, EXP e2)) = [*compileExp(e1), *compileExp(e2), sub()];
Instrs compileExp(add(EXP e1, EXP e2)) = [*compileExp(e1), *compileExp(e2), add()];
Instrs compileExp(gt(EXP e1, EXP e2)) = [*compileExp(e1), *compileExp(e2), gt()];
Instrs compileExp(lt(EXP e1, EXP e2)) = [*compileExp(e1), *compileExp(e2), lt()];
Instrs compileExp(ge(EXP e1, EXP e2)) = [*compileExp(e1), *compileExp(e2), ge()];
Instrs compileExp(le(EXP e1, EXP e2)) = [*compileExp(e1), *compileExp(e2), le()];
Instrs compileExp(eq(EXP e1, EXP e2)) = [*compileExp(e1), *compileExp(e2), eq()];
Instrs compileExp(neq(EXP e1, EXP e2)) = [*compileExp(e1), *compileExp(e2), neq()];

// å¼•æ•°ã®ã‚³ãƒ¼ãƒ‰ç”Ÿæˆã‚’ã—ã¦ã‚¹ã‚¿ãƒƒã‚¯ã«ãƒ—ãƒƒã‚·ãƒ¥ã™ã‚‹ã€‚ãã®å¾Œé–¢æ•°åã‚’ã‚¹ã‚¿ãƒƒã‚¯ã«ãƒ—ãƒƒã‚·ãƒ¥ã—call()ã‚’å‘¼ã¶ã€‚
Instrs compileExp(call(str name, list[EXP] args))
    = [*[*compileExp(arg) | arg <- args], pushId(name), call()];

// ã‚¹ã‚¿ãƒƒã‚¯ã®å€¤ã‚’å¤‰æ•°ã«æŸç¸›å¾Œã€expã®ã‚³ãƒ¼ãƒ‰ç”Ÿæˆã€‚
Instrs compileFunc(list[str] args, EXP exp)
    = [*[*[pushId(arg), setVal()] | arg <- args], *compileExp(exp)];

Code compileProg(prog (list[FUNC] funcs))
    = (name : compileFunc(args, exp)
        | func(str name, list[str] args, EXP exp) <- funcs);
```

æ‰€ã€…ã«`[*..., *...]`ã¿ãŸã„ã«`*`ãŒä»˜ãã®ãŒæ°—ã«ãªã‚‹ã¨æ€ã„ã¾ã™ã€‚ã“ã‚Œã¯é…åˆ—ã‚’å¹³å¦åŒ–ã™ã‚‹æ–‡æ³•ã§ã™ã€‚ä¾‹ãˆã°`[[1, 2], [3, 4]]`ã¯`list[list[int]]`å‹ã«ãªã‚‹ã®ã§ã™ãŒã€`[*[1, 2], *[3, 4]]`ã¨æ›¸ãã¨ã€ã“ã‚Œã¯`[1, 2, 3, 4]`ã«å±•é–‹ã•ã‚Œã¦å‹ã¯`list[int]`ã«ãªã‚Šã¾ã™ã€‚ã“ã®æ–‡æ³•ã‚’é§†ä½¿ã™ã‚‹ã¨ã€ã»ã¨ã‚“ã©ãŒãƒªã‚¹ãƒˆã®å®šç¾©ã¨å†…åŒ…è¡¨ç¾ã§æ›¸ã‘ã¾ã™ã€‚ç´ æ™´ã‚‰ã—ã„ï¼

æœ€å¾Œã«`Main.rsc`ã«ã“ã‚Œã‚‰ã®é–¢æ•°ã‚’å‘¼ã³å‡ºã™ãŸã‚ã®é–¢æ•°ã‚’å®šç¾©ã—ã¾ã™ã€‚

```rascal:Main.rsc
module Main

import Load;
import Compile;
import Assembly;

Code compile(str s) = compileProg(load(s));
Code compile(loc l) = compileProg(load(l));
```

## ä»Šå›å®Ÿè£…ã—ãŸã‚½ãƒ¼ã‚¹ã‚³ãƒ¼ãƒ‰

https://github.com/whtsht/rascal-func

`examples`ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªä¸‹ã«`fib.func`ã¨`fact.func`ãŒã‚ã‚Šã¾ã™ã€‚ã©ã®ã‚ˆã†ãªã‚³ãƒ¼ãƒ‰ãŒç”Ÿæˆã•ã‚Œã‚‹ã‹è¦‹ãŸã„å ´åˆã¯rascalã‚’REPLã‚’èµ·å‹•ã—ã¦ç¢ºèªã§ãã¾ã™ã€‚

```bash
$ rascal
rascal>import Main;
rascal>compile(|cwd:///examples/fib.func|);
map[str, list[Instr]]: ("fib":[
    pushId("n"),
    setVal(),
    pushId("n"),
    getVal(),
    pushNat(2),
    lt(),
    goIf("L1"),
    pushId("n"),
    getVal(),
    pushNat(1),
    sub(),
    pushId("fib"),
    call(),
    pushId("n"),
    getVal(),
    pushNat(2),
    sub(),
    pushId("fib"),
    call(),
    add(),
    go("L2"),
    label("L1"),
    pushId("n"),
    getVal(),
    label("L2")
  ])
```

## ãŠã‚ã‚Šã«

Rascalã§ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ã‚’ä½œã‚Šã¾ã—ãŸã€‚

é…åˆ—ã®å¹³å¦åŒ–ã‚„æŸ”è»Ÿãªãƒ‘ã‚¿ãƒ¼ãƒ³ãƒãƒƒãƒãªã©ãŒæ›¸ãã‚„ã™ã„ã ã‘ã§ã€ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©æ›¸ãã¨ãã®å¿«é©ã•ãŒä»–è¨€èªã¨ã‹ãªã‚Šé•ã†ã¨ç§ã¯æ€ã„ã¾ã—ãŸã€‚å®Ÿéš›ã«æ¯”è¼ƒã—ã¦ã¿ãŸã„ã§ã™ã€‚

ä»Šå›ã¯å¤‰æ•°ãŒå®£è¨€ã•ã‚Œã¦ã„ã‚‹ã‹ã©ã†ã‹ãªã©ã®ã‚½ãƒ¼ã‚¹ã‚³ãƒ¼ãƒ‰ã®æ¤œè¨¼ã‚’å…¨ãã—ã¦ã„ã¾ã›ã‚“ã€‚Rascalå…¬å¼ã‚µã‚¤ãƒˆã®[å‹ãƒã‚§ãƒƒã‚¯ã®ä¾‹](https://www.rascal-mpl.org/docs/Recipes/Languages/Pico/Typecheck/)ãªã©ã‚’å‚è€ƒã«ã™ã‚Œã°ã§ãã‚‹ã¨æ€ã†ã®ã§ã€èˆˆå‘³ã®ã‚ã‚‹æ–¹ã¯ãœã²ã€‚

## å‚è€ƒæ–‡çŒ®

https://www.rascal-mpl.org/docs/Recipes/Languages/Func/

https://www.rascal-mpl.org/docs/Recipes/Languages/Pico/
